import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    // Needed for Forge+Fabric
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
    // Kotlin
    id "org.jetbrains.kotlin.jvm" version "1.9.10"

    id "java"
}

archivesBaseName = rootProject.archives_base_name
version = rootProject.mod_version
group = rootProject.maven_group

architectury {
    minecraft = rootProject.minecraft_version
    platformSetupLoomIde()
    fabric()
}

loom {
    silentMojangMappingsLicense()

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=beyondthesky"

            runDir "build/datagen"
        }
    }
}

repositories {
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.org"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    // The following line declares the mojmap mappings, you may use other mappings as well
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modImplementation("net.fabricmc:fabric-language-kotlin:$rootProject.fabric_kotlin_version")


    modApi("org.valkyrienskies:valkyrienskies-1201-fabric:${rootProject.vs2_version}") {transitive = false}
    modApi("org.valkyrienskies:valkyrienskies-1201-common:${rootProject.vs2_version}")
    implementation("org.valkyrienskies.core:api:1.1.0+ead972f384") { transitive = false }


    modImplementation "maven.modrinth:modmenu:7.1.0"
    modImplementation "com.ptsmods:devlogin:3.3.2"
}


repositories {
    mavenLocal()
    maven {
        name = "Valkyrien Skies"
        url = 'https://maven.valkyrienskies.org'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = 17
    }
}